plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}


def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("key.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}



android {
    namespace "com.liyin.freeman"
    compileSdkVersion flutter.compileSdkVersion
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    defaultConfig {
        // Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "com.liyin.freeman"
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-gradle-build-configuration.
        minSdkVersion 24
        targetSdkVersion flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName

        ndk {
            //abiFilters "armeabi-v7a", "arm64-v8a"
            abiFilters "arm64-v8a"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    afterEvaluate {
        tasks.matching { it.name.startsWith("assemble") }.all { task ->
            task.doLast {
                def apkDir = file("$buildDir/outputs/flutter-apk")
                def files = apkDir.listFiles().findAll { it.name.endsWith("-release.apk") }
                if (files.isEmpty()) {
                    println "⚠️ No APK found to rename."
                    return
                }

                def file = files.first()
                def versionName = android.defaultConfig.versionName
                def versionCode = android.defaultConfig.versionCode
                def date = new Date().format("yyyyMMdd_HHmm")
                def newName = "freeman_v${versionName}_${versionCode}_${date}.apk"
                def renamed = new File(file.parent, newName)

                // ✅ Groovy 的文件复制方法
                renamed.bytes = file.bytes

                println "✅ APK copied as: ${renamed.name}"
            }
        }
    }



}

flutter {
    source '../..'
}
